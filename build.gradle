plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'io.github.0ffz.github-packages' version '[1,2)'
    id 'com.matthewprenger.cursegradle' version '[1.4,1.5)'
    id 'com.modrinth.minotaur' version '[2,3)'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = "${minecraft_version}-${mod_version}-fabric"
group = mod_group

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

loom {
    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

processResources {
    exclude '.cache/'
}

//configurations {
//    runtimeClasspath.extendsFrom localRuntime
//}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.property("neogradle.subsystems.parchment.minecraftVersion")}:${project.property("neogradle.subsystems.parchment.mappingsVersion")}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "com.aetherteam.aether:aether:${project.aether_version}"
    modImplementation "com.aetherteam.nitrogen:nitrogen_internals:${project.nitrogen_version}"
    modImplementation "io.wispforest:accessories-fabric:${project.accessories_version}+${project.minecraft_version}"

    implementation("io.wispforest:endec:${rootProject.endec_version}")

    modImplementation("io.wispforest:owo-lib:${project.owo_version}")

    modRuntimeOnly("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${project.forge_config_version}")
}

repositories {
    maven { url = "https://maven.wispforest.io" }
    maven { url = "https://maven.wispforest.io/releases" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.fabricmc.net" }
    maven { url = "https://maven.su5ed.dev/releases" }
    maven { url = "https://maven.architectury.dev/" }
    maven githubPackage.invoke("The-Aether-Team/The-Aether")
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    maven { url = "https://maven.parchmentmc.org" }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            mod_license            : mod_license,
            mod_url                : mod_url,
            mod_update             : mod_update,
            mod_tracker            : mod_tracker,
            mod_credits            : mod_credits,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            fabric_version         : fabric_version,
            fabric_version_range   : fabric_version_range,
            fabric_loader_version_range : fabric_loader_version_range,
            aether_version_range   : aether_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json']) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.compilerArgs << "-Xmaxerrs" << "9000"
}

tasks.register('sourceJar', Jar) {
    dependsOn 'classes'
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

compileJava {
    options.incremental = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            artifact project.tasks.jar
            artifact project.sourceJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/The-Aether-Team/Protect-Your-Moa"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

curseforge {
    project {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        id = "${project.curseforge_id}"
        releaseType = "${project.version_type}"
        changelog = file("$rootDir/docs/CHANGELOG.md")
        changelogType = "markdown"
        addGameVersion("${project.minecraft_version}")
        addGameVersion("NeoForge")
        mainArtifact(tasks.jar) {
            displayName = "${project.mod_name} - ${version}"
            relations {
                requiredDependency "aether"
            }
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${project.modrinth_id}"
    versionNumber = "${version}"
    versionType = "${project.version_type}"
    versionName = "${project.mod_name} - ${version}"
    changelog = new File("$rootDir/docs/CHANGELOG.md").text
    uploadFile = tasks.jar
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["neoforge"]
    dependencies {
        required.version "aether", "${project.aether_version}"
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}